<?xml version="1.0" encoding="utf-8"?>
<s:SkinnableContainer xmlns:fx="http://ns.adobe.com/mxml/2009" 
					  xmlns:s="library://ns.adobe.com/flex/spark" width="100%" height="100%"
					  backgroundColor="#ffffff" xmlns:components="components.*" >
	<s:layout>
		<s:VerticalLayout/>
	</s:layout>
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	
	
	<fx:Script>
		<![CDATA[
			import components.alerts.AlertPopUp_CloseProject;
			
			import mx.managers.PopUpManager;
			
			import net.fastindemand.data.AppData;
			import net.fastindemand.dispatcher.Dispatcher;
			import net.fastindemand.event.AppEvent;
			
			// Alert window.
			private var alertClose:AlertPopUp_CloseProject = new AlertPopUp_CloseProject;
			
			
			public function init():void
			{
				
				//creationComplete="init()" 
				
				trace("ProjectMaster - init");
				Dispatcher.addEventListener( AppEvent.NEW_PROJECT, onNewProject );
				Dispatcher.addEventListener( AppEvent.OPEN_PROJECT, onOpenProject );
				alertClose.addEventListener( AppEvent.CLOSE_PROJECT, closeProject );

			}
			
			
			protected function onNewProject(e:Event):void
			{
				
				trace("ProjectMaster - onNewProject");
				var d:Date = new Date;
				AppData.PID = String( d.time );
				docTitle.text = String( "Untitled " + AppData.projectsXML.children().length() );
				docContent.text = "";
				
				// Createfolder.
				var folder:File = File.documentsDirectory.resolvePath("LyricsToGo/" + AppData.PID );
				folder.createDirectory();
				
				// Temporarily kill Save button.
				saveButton.enabled = false;
				
			}
			
			
			protected function onOpenProject(e:Event):void
			{
				
				trace("ProjectMaster - onOpenProject");
				var xml:XML = XML( AppData.projectsXML.children()[ AppData.projectIndex ] );
				AppData.PID = xml.PID;
				docTitle.text = xml.title;
				docContent.text = xml.content;
				
				// Temporarily kill Save button.
				saveButton.enabled = false;
				
			}
			
			
			protected function saveText():void
			{
				
				trace("ProjectMaster - saveText");
				var xmllist:XMLList = AppData.projectsXML..project;
				
				// Need to check if the PID exists.
				if( xmllist.( PID == AppData.PID ).toString().length > 0 )
				{
					
					//trace("Proj exists!");
					xmllist.( PID == AppData.PID ).title = docTitle.text;
					xmllist.( PID == AppData.PID ).content = docContent.text;
					
				}
				else
				{
					
					//trace("Proj does not exists. Create.");
					// Create the XMLList to be added.
					xmllist = new XMLList(<project></project>);
					xmllist.title = docTitle.text;
					xmllist.content = docContent.text;
					xmllist.PID = AppData.PID;
					AppData.projectsXML.prependChild( xmllist );
					
				}
				
				// This is the Strign that will get saved to the File.
				var outputString:String = '<?xml version="1.0" encoding="utf-8"?>\n';
				outputString += AppData.projectsXML.toXMLString();
				outputString = outputString.replace( /\n/g, File.lineEnding );
				
				// Saving the actual file.
				AppData.stream = new FileStream();
				AppData.stream.open( AppData.currentProjectsFile, FileMode.WRITE );
				AppData.stream.writeUTFBytes( outputString );
				AppData.stream.close();
				
				// Alert everyone else that the XML has been saved and changed.
				Dispatcher.dispatchEvent( new AppEvent( AppEvent.UPDATE_PROJECTS ) );
				
				// Temporarily kill Save button.
				saveButton.enabled = false;
				
			}
			
			
			// Called when a textfield's content changes.
			protected function onProjectChange(e:Event=null):void
			{
				
				//trace("ProjectMaster - onProjectChange");
				saveButton.enabled = true;
				
			}
			
			
			protected function initCloseProject():void
			{
				
				//trace("ProjectMaster - initCloseProject");
				
				if( saveButton.enabled )
				{
					
					alertClose.open( this, true );
					alertClose.height = this.height
					alertClose.width = this.width;
					
					// Center the pop-up in the parent container.
					PopUpManager.centerPopUp(alertClose);
					
				}
				else
				{
					
					// project is already saved.
					closeProject();
					
				}
				
				
			}
			
			
			protected function closeProject(e:Event=null):void
			{
				
				//trace("ProjectMaster - closeProject");
				Dispatcher.dispatchEvent( new AppEvent( AppEvent.CLOSE_PROJECT, true ) );
				
			}
			
			
			public function clear():void
			{
				
				trace("ProjectMaster - clear");
				docTitle.text = "";
				docContent.text = "";
				
			}
			
		]]>
	</fx:Script>
	
<!--	<components:AlertPopUp id="alert" width="100%" height="100%"/>-->
	<s:HGroup verticalAlign="middle" horizontalAlign="right" paddingBottom="5" paddingLeft="5" paddingRight="5" paddingTop="5" width="100%">
		
		
		
		
		<s:Label>Title</s:Label>
		<s:TextInput id="docTitle" width="100%" height="40" needsSoftKeyboard="true" change="onProjectChange()" />
		<s:Button
			id="saveButton" label="Save"
			click="saveText();"
			/>
		<s:Button
			id="closeButton" label="Close"
			click="initCloseProject()"
			/>
	</s:HGroup>
	
	<s:TextArea id="docContent" width="100%" height="100%" needsSoftKeyboard="true" change="onProjectChange()" />
	<s:Label id="myLabel" width="180" fontWeight="bold" fontSize="24"/>
	
	
</s:SkinnableContainer>
